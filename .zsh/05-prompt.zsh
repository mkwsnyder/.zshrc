#                 ‚ñà‚ñà
#                ‚ñë‚ñà‚ñà
#  ‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà  ‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà‚ñë‚ñà‚ñà
# ‚ñë‚ñë‚ñë‚ñë‚ñà‚ñà  ‚ñà‚ñà‚ñë‚ñë‚ñë‚ñë ‚ñë‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà
#    ‚ñà‚ñà  ‚ñë‚ñë‚ñà‚ñà‚ñà‚ñà‚ñà ‚ñë‚ñà‚ñà‚ñë‚ñë‚ñë‚ñà‚ñà
#   ‚ñà‚ñà    ‚ñë‚ñë‚ñë‚ñë‚ñë‚ñà‚ñà‚ñë‚ñà‚ñà  ‚ñë‚ñà‚ñà
#  ‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà ‚ñà‚ñà‚ñà‚ñà‚ñà‚ñà ‚ñë‚ñà‚ñà  ‚ñë‚ñà‚ñà
# ‚ñë‚ñë‚ñë‚ñë‚ñë‚ñë ‚ñë‚ñë‚ñë‚ñë‚ñë‚ñë  ‚ñë‚ñë   ‚ñë‚ñë
#
# prompt

# allow functions in the prompt
setopt PROMPT_SUBST
autoload -Uz colors && colors

# ===== ICONS =====

# may require this font (note, you shouldn't need to change your terminal font, it should fallback for the icons) https://github.com/romkatv/powerlevel10k#manual-font-installation (aur/ttf-meslo-nerd-font-powerlevel10k)
# (if the above doesn't work) may require the awesome-terminal-fonts package (https://github.com/gabrielelana/awesome-terminal-fonts, aur/awesome-terminal-fonts) and/or a nerd font (https://www.nerdfonts.com/)
# full list of icons (don't view this file via the web browser, icons may not show up properly) https://raw.githubusercontent.com/romkatv/powerlevel10k/master/internal/icons.zsh

ICO_OS="ÔåÉ"

ICO_VCS="ÔÑ¶"
# ICO_VCS='ÓÇ†'
# ICO_VCS='Ôáì'

# ICO_BEHIND="‚Üì"
# ICO_BEHIND="Óú•"
# ICO_BEHIND="ÔÑ¶"
ICO_BEHIND="ÔÄö"
# ICO_BEHIND="‚ñº"

# ICO_AHEAD="‚Üë"
# ICO_AHEAD="Óúß"
ICO_AHEAD="ÔÄõ"
# ICO_AHEAD="‚ñ≤"

ICO_PUSH_BEHIND="‚á†"
# ICO_PUSH_BEHIND="‚á†"

ICO_PUSH_AHEAD="‚á¢"
# ICO_PUSH_AHEAD="‚á¢"

ICO_STASHED="ÔÄú"
# ICO_STASHED="*"

ICO_CONFLICTED="‚úò"
# ICO_CONFLICTED="~"

ICO_STAGED="ÔÅï"
# ICO_STAGED="+"

ICO_UNSTAGED="ÔÅ™"
# ICO_UNSTAGED="!"

ICO_UNTRACKED="ÔÅô"
# ICO_UNTRACKED="?"

# ICO_DIRTY="*"
# ICO_DIRTY="‚ÜØ"
ICO_DIRTY="‚ö°"

ICO_DIVERGED="‚Üï"
# ICO_DIVERGED="ÔêÇ"
# ICO_DIVERGED="Ô≠Ä"

# ===== END ICONS =====

# ===== COLORS =====

# color reference: https://www.ditig.com/256-colors-cheat-sheet

COLOR_PATH="15" # white

# NOTE: root colors will only work if .zshrc and the files in .zsh are also in /root/ (you can symlink them)
if [[ "$EUID" -ne "0" ]]
then  # if user is not root
  if [ "$HOST" = "roshar" ] # teal
  then
    COLOR_USER="36" # teal 36
    COLOR_HOST="43" # teal 43
    COLOR_GIT="30" # teal 30
  fi
  if [ "$HOST" = "scadrial" ] # blue
  then
    COLOR_USER="27" # blue 27
    COLOR_HOST="33" # blue 33
    COLOR_GIT="25" # blue 25
  fi
  if [ "$HOST" = "braize" ] # purple
  then
    COLOR_USER="57" # purple 57
    COLOR_HOST="93" # purple 93
    COLOR_GIT="55" # purple 55
  fi
else # root, red
  COLOR_USER="88"
  COLOR_HOST="124"
  COLOR_GIT="52"
fi

# ===== END COLORS =====

# ===== BASIC GIT PROMPT =====

# basic git prompt, doesn't require additional plugins
# can be used by adding `$(GIT_PROMPT)` to your prompt
# pulled form here (has been modified): https://github.com/xero/dotfiles/blob/master/zsh/.zsh/05-prompt.zsh
GIT_PROMPT() {
  test=$(git rev-parse --is-inside-work-tree 2> /dev/null)
  if [ ! "$test" ]
  then
      echo "$reset_color%F{white}ÓÇ∞"
      return
  fi
  ref=$(git name-rev --name-only HEAD | sed 's!remotes/!!;s!undefined!merging!' 2> /dev/null)
  ref2=$(git symbolic-ref HEAD 2> /dev/null | awk 'BEGIN{FS="/"} {print $NF}')
  dirty="" && [[ $(git diff --shortstat 2> /dev/null | tail -n1) != "" ]] && dirty=$ICO_DIRTY
  stat=$(git status | sed -n 2p)
  case "$stat" in
    *ahead*)
      stat=$ICO_AHEAD
    ;;
    *behind*)
      stat=$ICO_BEHIND
    ;;
    *diverged*)
      stat=$ICO_DIVERGED
    ;;
    *)
      stat=""
    ;;
  esac
  echo "%K{${COLOR_GIT}}%F{white}ÓÇ∞ ${ICO_VCS} ${ref2} ${dirty}${stat} $reset_color%F{${COLOR_GIT}}ÓÇ∞"
}

# ===== END BASIC GIT PROMPT =====

# ===== FULL GIT PROMPT =====

# USAGE
# clone the gitstatus repo: https://github.com/romkatv/gitstatus
# source the plugin like so: `source ~/gitstatus/gitstatus.plugin.zsh`
# can be used by adding `${GITSTATUS_PROMPT}` to your prompt

# NOTE: if you are not going to use the gitstatus prompt, comment the function out
# for more info, see https://github.com/romkatv/gitstatus

# TODO: make gitstatus prompt faster as described here (asynchronously vs synchronously): https://github.com/romkatv/gitstatus#using-from-zsh (scroll to end of the section)
# function pulled from the example prompt (has been modified): https://github.com/romkatv/gitstatus/blob/master/gitstatus.prompt.zsh
function gitstatus_prompt_update() {
  emulate -L zsh
  typeset -g  GITSTATUS_PROMPT=''
  typeset -gi GITSTATUS_PROMPT_LEN=0

  # what's shown if not in a git repo
  GITSTATUS_PROMPT="$reset_color%${COLOR_PATH}FÓÇ∞"

  # Call gitstatus_query synchronously. Note that gitstatus_query can also be called
  # asynchronously; see documentation in gitstatus.plugin.zsh.
  gitstatus_query 'MY'                  || return 1  # error
  [[ $VCS_STATUS_RESULT == 'ok-sync' ]] || return 0  # not a git repo

  local      clean='%15F'   # ~~green~~ white foreground | green: 76
  local   modified='%178F'  # yellow foreground
  local  untracked='%39F'   # blue foreground
  local conflicted='%196F'  # red foreground

  local p="%${COLOR_PATH}F%${COLOR_GIT}KÓÇ∞%f ${ICO_VCS} "

  local where  # branch name, tag or commit
  if [[ -n $VCS_STATUS_LOCAL_BRANCH ]]; then
    where=$VCS_STATUS_LOCAL_BRANCH
  elif [[ -n $VCS_STATUS_TAG ]]; then
    p+='%f#'
    where=$VCS_STATUS_TAG
  else
    p+='%f@'
    where=${VCS_STATUS_COMMIT[1,8]}
  fi

  (( $#where > 32 )) && where[13,-13]="‚Ä¶"  # truncate long branch names and tags
  p+="${clean}${where//\%/%%}"             # escape %

  # ‚á£42 if behind the remote.
  (( VCS_STATUS_COMMITS_BEHIND )) && p+=" ${clean}${ICO_BEHIND}${VCS_STATUS_COMMITS_BEHIND}"
  # ‚á°42 if ahead of the remote; no leading space if also behind the remote: ‚á£42‚á°42.
#   (( VCS_STATUS_COMMITS_AHEAD && !VCS_STATUS_COMMITS_BEHIND )) && p+=" "
  (( VCS_STATUS_COMMITS_AHEAD  )) && p+=" ${clean}${ICO_AHEAD}${VCS_STATUS_COMMITS_AHEAD}"
  # ‚á†42 if behind the push remote.
  (( VCS_STATUS_PUSH_COMMITS_BEHIND )) && p+=" ${clean}${ICO_PUSH_BEHIND}${VCS_STATUS_PUSH_COMMITS_BEHIND}"
  (( VCS_STATUS_PUSH_COMMITS_AHEAD && !VCS_STATUS_PUSH_COMMITS_BEHIND )) && p+=" "
  # ‚á¢42 if ahead of the push remote; no leading space if also behind: ‚á†42‚á¢42.
  (( VCS_STATUS_PUSH_COMMITS_AHEAD  )) && p+=" ${clean}${ICO_PUSH_AHEAD}${VCS_STATUS_PUSH_COMMITS_AHEAD}"
  # *42 if have stashes.
  (( VCS_STATUS_STASHES        )) && p+=" ${clean}${ICO_STASHED}${VCS_STATUS_STASHES}"
  # 'merge' if the repo is in an unusual state.
  [[ -n $VCS_STATUS_ACTION     ]] && p+="  ${conflicted}${VCS_STATUS_ACTION}"
  # ~42 if have merge conflicts.
  (( VCS_STATUS_NUM_CONFLICTED )) && p+=" ${conflicted}${ICO_CONFLICTED}${VCS_STATUS_NUM_CONFLICTED}"
  # +42 if have staged changes.
  (( VCS_STATUS_NUM_STAGED     )) && p+=" ${modified}${ICO_STAGED}${VCS_STATUS_NUM_STAGED}"
  # !42 if have unstaged changes.
  (( VCS_STATUS_NUM_UNSTAGED   )) && p+=" ${modified}${ICO_UNSTAGED}${VCS_STATUS_NUM_UNSTAGED}"
  # ?42 if have untracked files. It's really a question mark, your font isn't broken.
  (( VCS_STATUS_NUM_UNTRACKED  )) && p+=" ${untracked}${ICO_UNTRACKED}${VCS_STATUS_NUM_UNTRACKED}"

  p+=" $reset_color%F{${COLOR_GIT}}ÓÇ∞"

  GITSTATUS_PROMPT="${p}%f"

  # The length of GITSTATUS_PROMPT after removing %f and %F.
  GITSTATUS_PROMPT_LEN="${(m)#${${GITSTATUS_PROMPT//\%\%/x}//\%(f|<->F)}}"
}

# Start gitstatusd instance with name "MY". The same name is passed to
# gitstatus_query in gitstatus_prompt_update. The flags with -1 as values
# enable staged, unstaged, conflicted and untracked counters.
gitstatus_stop 'MY' && gitstatus_start -s -1 -u -1 -c -1 -d -1 'MY'

# On every prompt, fetch git status and set GITSTATUS_PROMPT.
autoload -Uz add-zsh-hook
add-zsh-hook precmd gitstatus_prompt_update

# Enable/disable the right prompt options.
setopt no_prompt_bang prompt_percent prompt_subst

# ===== END FULL GIT PROMPT =====

# ===== PROMPT =====

# visual effects reference: https://man.archlinux.org/man/zshmisc.1#Visual_effects

PROMPT='%${COLOR_USER}K %f%n %${COLOR_USER}F%${COLOR_HOST}KÓÇ∞ %f%m %${COLOR_HOST}F%${COLOR_PATH}KÓÇ∞ %${COLOR_TEXT_BG_LIGHT}F%~ ${GITSTATUS_PROMPT}$reset_color%f%k'
# PROMPT+=$'\n%${COLOR_USER}Fü≠ì‚ñà‚ñì‚ñí‚ñë%f '
prompt+=$'\n%${COLOR_USER}F‚ù±%f '

# old prompt
# PROMPT='%K{36} %n %F{36}%K{43}ÓÇ∞ %f%m %F{43}%K{white}ÓÇ∞ %F{black}%~ $(GIT_PROMPT)$reset_color%f%k
# %F{36}‚ñà‚ñà‚ñì‚ñí‚ñë%f '
